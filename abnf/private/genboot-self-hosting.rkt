#lang racket/base
;; Generates boot.rkt by writing out the rule lists from the two
;; grammars in the rfc5234/ directory. NOTE WELL: Requires boot.rkt to
;; be runnable and complete! See `genboot.rkt` for the hand-translated
;; data needed to kickstart the self hosting.

(module+ main
  (require racket/pretty)
  (require (prefix-in : abnf/rfc5234/ast))
  (require (rename-in (submod abnf/rfc5234/abnf-rules rulelist) [%rulelist abnf-rulelist]))
  (require (rename-in (submod abnf/rfc5234/core-rules rulelist) [%rulelist core-rulelist]))
  (define rulelist
    (:rulelist (filter :rule?
                       (append (:rulelist-rules abnf-rulelist)
                               (:rulelist-rules core-rulelist)))))
  (with-output-to-file "boot.rkt"
    (lambda ()
      (printf "#lang racket\n")
      (printf ";; Generated by abnf/private/genboot-self-hosting.rkt.\n")
      (printf "(require abnf/lang)\n")
      (pretty-write (syntax->datum #`(_define-and-provide-abnf #,rulelist))))))
